function(check_vulkan_version)
    message(STATUS "Ensuring the currently installed driver supports the Vulkan version requested by the Vulkan-Header.")
    cmake_parse_arguments(VULKAN_CHECK_VERSION "" "INCLUDE_DIR" "" ${ARGN})

    # Get the current Vulkan-Header version (e.g. 1.2.189).
    # This snippet is based on: https://gitlab.kitware.com/cmake/cmake/-/blob/v3.23.1/Modules/FindVulkan.cmake#L140-156
    if(VULKAN_CHECK_VERSION_INCLUDE_DIR)
        set(VULKAN_CORE_H ${VULKAN_CHECK_VERSION_INCLUDE_DIR}/vulkan/vulkan_core.h)
        if(EXISTS ${VULKAN_CORE_H})
            file(STRINGS  ${VULKAN_CORE_H} VulkanHeaderVersionLine REGEX "^#define VK_HEADER_VERSION ")
            string(REGEX MATCHALL "[0-9]+" VulkanHeaderVersion "${VulkanHeaderVersionLine}")
            file(STRINGS  ${VULKAN_CORE_H} VulkanHeaderVersionLine2 REGEX "^#define VK_HEADER_VERSION_COMPLETE ")
            string(REGEX MATCHALL "[0-9]+" VulkanHeaderVersion2 "${VulkanHeaderVersionLine2}")
            list(LENGTH VulkanHeaderVersion2 _len)
            # Versions >= 1.2.175 have an additional numbers in front of e.g. '0, 1, 2' instead of '1, 2'
            if(_len EQUAL 3)
                list(REMOVE_AT VulkanHeaderVersion2 0)
            endif()
            list(APPEND VulkanHeaderVersion2 ${VulkanHeaderVersion})
            list(JOIN VulkanHeaderVersion2 "." VULKAN_HEADER_VERSION)
        else()
            message(FATAL_ERROR "'${VULKAN_CORE_H}' does not exist. Try calling 'find_package(Vulkan REQUIRED)' before you call this function or set 'Vulkan_INCLUDE_DIR' manually!")
            return()
        endif()
    else()
        message(FATAL_ERROR "Invalid Vulkan include directory given. Try calling 'find_package(Vulkan REQUIRED)' before you call this function or set 'Vulkan_INCLUDE_DIR' manually!")
        return()
    endif()
    message(STATUS "Found Vulkan Header version ${VULKAN_HEADER_VERSION}.")

    # Get Vulkan version supported by driver
    find_program(VULKAN_INFO_PATH NAMES vulkaninfo)
    if(${VULKAN_INFO_PATH})
        message(FATAL_ERROR "vulkaninfo not found. The Vulkan SDK might not be installed properly.")
        return()
    endif()

    execute_process(COMMAND "vulkaninfo"
                    OUTPUT_VARIABLE VULKAN_INFO_OUTPUT)
    if(${VULKAN_INFO_OUTPUT} MATCHES ".*Vulkan version ([0-9]+.[0-9]+.[0-9]+).*")
        set(VULKAN_DRIVER_VERSION ${CMAKE_MATCH_1})
        message(STATUS "vulkaninfo reported supported version ${VULKAN_DRIVER_VERSION}")
    else()
        message(FATAL_ERROR "No valid version found in vulkaninfo. Does your GPU (driver) support Vulkan?")
        return()
    endif()

    # Compare driver and header version
    if(${VULKAN_DRIVER_VERSION} VERSION_LESS ${VULKAN_HEADER_VERSION})
        # Version missmatch. Let us check if the major version is the same.
        if(${VULKAN_DRIVER_VERSION} MATCHES "[0-9]+.([0-9]+).[0-9]+")
            set(VULKAN_DRIVER_MAJOR_VERSION ${CMAKE_MATCH_1})
        else()
            message(FATAL_ERROR "Invalid Vulkan driver version '${VULKAN_DRIVER_VERSION}' found. Expected version in the following format: '[0-9]+.[0-9]+.[0-9]+'")
        endif()
        if(${VULKAN_HEADER_VERSION} MATCHES "[0-9]+.([0-9]+).[0-9]+")
            set(VULKAN_HEADER_MAJOR_VERSION ${CMAKE_MATCH_1})
        else()
            message(FATAL_ERROR "Invalid Vulkan-Header version '${VULKAN_HEADER_VERSION}' found. Expected version in the following format: '[0-9]+.[0-9]+.[0-9]+'")
        endif()

        if(${VULKAN_DRIVER_MAJOR_VERSION} EQUAL ${VULKAN_HEADER_MAJOR_VERSION})
            message(WARNING "Your GPU driver does not support Vulkan > ${VULKAN_DRIVER_VERSION}, but you try to use Vulkan-Header ${VULKAN_HEADER_VERSION}. At least your driver supports the same major version (${VULKAN_DRIVER_MAJOR_VERSION}), so this should be fine but keep it in mind if encounter some strange behavior.")
        else()
            message(FATAL_ERROR "Your GPU driver does not support Vulkan > ${VULKAN_DRIVER_VERSION}, but you try to use Vulkan-Header ${VULKAN_HEADER_VERSION}.")
        endif()
    else()
        message(STATUS "Vulkan version check successful!")
    endif()
endfunction()
